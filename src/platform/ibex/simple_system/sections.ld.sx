/*
 * CYANCORE LICENSE
 * Copyrights (C) 2024, Cyancore Team
 *
 * File Name		: sections.ld.sx
 * Description		: This file contains memory layout for the
 *			  cyancore framework
 * Primary Author	: Akash Kollipara [akashkollipara@gmail.com]
 * Organisation		: Cyancore Core-Team
 */

#include <plat_mem.h>
#include <linker_macros.h>

MEMORY
{
	/* VM Addresses */
	ram (airwx)	: ORIGIN = RAM_START, LENGTH = RAM_LENGTH
}

ENTRY(entry)

SECTIONS
{
	.text : ALIGN(4)
	{
		KEEP(*(.vtors))
		KEEP(*(.text.entry))
		*(.text)
		*(.text.*)
		. = ALIGN(8);
	} > ram

	.rodata : ALIGN(4)
	{
		*(.version)
		KEEP(*(.version))
		KEEP(*(.rdata))
		KEEP(*(.rodata))
		*(.rodata.*)
		*(.srodata)
		*(.srodata.*)
		KEEP(*(.srodata))
		. = ALIGN(8);
	} > ram

	.bss : ALIGN(4)
	{
		*(.bss)
		*(.bss.*)
		KEEP(*(.bss))
		*(COMMON)
	} > ram

	.static_bss_global : ALIGN(4)
	{
		*(.sbss)
		*(.sbss.*)
		KEEP(*(.sbss))
	} > ram

	.static_global_var :
	{
	} > ram

	.static_data_global : ALIGN(4)
	{
		*(.sdata)
		*(.sdata.*)
		KEEP(*(.sdata))
	} > ram

	.data : ALIGN(4)
	{
		*(.data)
		*(.data.*)
		KEEP(*(.data))
		DRIVER_TABLE
		VCALL_TABLE
	} > ram

	.heap : ALIGN(HEAP_ALIGN)
	{
		*(.heap)
		KEEP(*(.heap))
		. = . + HEAP_SIZE;
	} > ram

	.stack : ALIGN(16)
	{
		*(.stack)
		KEEP(*(.stack))
		. = . + STACK_SIZE;
	} > ram

	PROVIDE(_text_start = ADDR(.text));
	PROVIDE(_text_size = SIZEOF(.text));
	PROVIDE(_text_end = _text_start + _text_size - 1);

	PROVIDE(_rodata_start = ADDR(.rodata));
	PROVIDE(_rodata_size = SIZEOF(.rodata));
	PROVIDE(_rodata_end = _rodata_start + _rodata_size - 1);

	PROVIDE(_tls_start = ADDR(.data));
	PROVIDE(_data_size = SIZEOF(.data) + SIZEOF(.static_data_global));
	PROVIDE(_data_vstart = ADDR(.static_data_global));
	PROVIDE(_data_vend = _data_vstart + _data_size - 1);

	PROVIDE(_global_start = ADDR(.static_global_var) + 0x800);
	PROVIDE(_stack_size = SIZEOF(.stack));
	PROVIDE(_stack_end = ADDR(.stack));
	PROVIDE(_stack_start = _stack_end + _stack_size);

	PROVIDE(_bss_start = ADDR(.bss));
	PROVIDE(_bss_size = SIZEOF(.bss) + SIZEOF(.static_bss_global));
	PROVIDE(_bss_end = _bss_start + _bss_size - 1);

	PROVIDE(_heap_start = ADDR(.heap));
	PROVIDE(_heap_size = SIZEOF(.heap));
	PROVIDE(_heap_end = _heap_start + _heap_size - 1);

	PROVIDE(_ram_size = _bss_size + _data_size + SIZEOF(.stack) + \
			SIZEOF(.heap) + SIZEOF(.text) + SIZEOF(.rodata));

	ASSERT((_ram_size < RAM_LENGTH), "< x > RAM size exceeded ...")

	/DISCARD/ : { *(.comment .trampolines) }
}
